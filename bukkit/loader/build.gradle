import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
}

dependencies {
    compileOnly 'com.destroystokyo.paper:paper-api:1.15.2-R0.1-SNAPSHOT'

    implementation project(':api')
    implementation project(':common:loader-utils')
}

build {
    dependsOn(":bukkit:build")
}

tasks.register('processSource', Sync) {
    outputs.upToDateWhen { false }
    from sourceSets.main.java

    filter(ReplaceTokens,
            tokens: [
                    mod_id  : project.ext.modId,
                    mod_name: project.ext.modName,
                    version : project.ext.fullVersion
            ]
    )

    into "$buildDir/src"
}

compileJava {
    source = processSource.outputs
}

processResources {
    outputs.upToDateWhen { false }
    // include platform specific files into processing
    from "${project.ext.dirtcoreDir}/common/loader-utils/src/main/resources"

    filesMatching('**/plugin.yml') {
        expand 'mod_id': project.ext.modId,
                'mod_name': project.ext.modName,
                'mod_authors': project.ext.modAuthors,
                'mod_description': project.ext.modDescription,
                'version': project.ext.fullVersion
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                "Specification-Title"           : project.ext.modId,
                "Specification-Vendor"          : project.ext.modAuthors,
                "Specification-Version"         : project.ext.majorVersion,
                "Implementation-Title"          : project.ext.modName,
                "Implementation-Version"        : project.ext.fullVersion,
                "Implementation-Vendor"         : project.ext.modAuthors,
                "Implementation-Timestamp"      : new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "paperweight-mappings-namespace": "mojang"
        ])
    }
}

shadowJar {
    archiveFileName = "DirtCore-Bukkit-${project.ext.fullVersion}.jar"

    from {
        project(':bukkit').tasks.shadowJar.archiveFile
    }

    dependencies {
        include(dependency('net.dirtcraft:.*'))
    }

    exclude '**/mods.toml'
    exclude '**/mcmod.info'
}

artifacts {
    archives shadowJar
}
