import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    alias(libs.plugins.neoforgeduserdev)
    alias(libs.plugins.shadow)
}

allprojects {
    sourceCompatibility = 21
    targetCompatibility = 21

    project.ext.minecraftVersion = minecraft_version
    project.ext.loaderVersionRange = loader_version_range
    java.toolchain.languageVersion = JavaLanguageVersion.of(21)
}

dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"

    compileOnly 'net.dirtcraft:common-loader-utils'
    implementation 'net.dirtcraft:common'
    implementation project(':neoforge_1_21_1:neoforge-api')
}

tasks.register('processSource', Sync) {
    outputs.upToDateWhen { false }
    from sourceSets.main.java

    filter(ReplaceTokens,
            tokens: [
                    mod_id           : project.ext.modId,
                    mod_name         : project.ext.modName,
                    version          : project.ext.fullVersion,
                    minecraft_version: project.ext.minecraftVersion
            ]
    )

    into "$buildDir/src"
}

compileJava {
    source = processSource.outputs
}

shadowJar {
    archiveFileName = "DirtCore-NeoForge-${minecraft_version}.jarinjar"

    dependencies {
        include(dependency('de.alphaconqueror:.*'))
        include(dependency('net.dirtcraft:.*'))
        include(dependency('net.dirtcraft.dirtbot:.*'))
    }

    relocate 'net.bytebuddy', 'net.dirtcraft.dirtcore.lib.bytebuddy'
    relocate 'net.kyori.adventure', 'net.dirtcraft.dirtcore.lib.adventure'
    relocate 'net.kyori.event', 'net.dirtcraft.dirtcore.lib.event'
    relocate 'com.github.benmanes.caffeine', 'net.dirtcraft.dirtcore.lib.caffeine'
    relocate 'com.zaxxer.hikari', 'net.dirtcraft.dirtcore.lib.hikari'
    relocate 'com.typesafe', 'net.dirtcraft.dirtcore.lib.typesafe'
    relocate 'de.alphaconqueror', 'net.dirtcraft.dirtcore.lib.alphaconqueror'
    //relocate 'javax.persistence', 'net.dirtcraft.dirtcore.libs.persistence'
    relocate 'net.dv8tion.jda', 'net.dirtcraft.dirtcore.lib.jda'
    relocate 'ninja.leaping.configurate', 'net.dirtcraft.dirtcore.lib.configurate'
    //relocate 'org.hibernate', 'net.dirtcraft.dirtcore.lib.hibernate'
    relocate 'org.mariadb.jdbc', 'net.dirtcraft.dirtcore.lib.mariadb'
}

artifacts {
    archives shadowJar
}
